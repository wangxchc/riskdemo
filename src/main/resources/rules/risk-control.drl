package rules

import java.time.LocalDate
import com.nongxinyin.demo.model.Transaction
import com.nongxinyin.demo.model.DailySummary
import redis.clients.jedis.Jedis
import com.nongxinyin.demo.pool.MyJedisPool
import java.text.DecimalFormat

global com.nongxinyin.demo.pool.MyJedisPool myRedisPool

declare Transaction
    @role(event)
end

rule "Initialize Daily Summary"
when
    $t: Transaction(
        $userId: userId,
        $date: timestamp.toLocalDate()
    )
    not DailySummary(userId == $userId, date == $date)
then
    String key = "daily:" + $userId + ":" + $date;
    Jedis jedis = null;
    Double total = 0.00;
    try{
        jedis = myRedisPool.getResource();
        total = jedis.incrByFloat(key, 0.00);
        insert(new DailySummary($userId, $date, total));
        jedis.expire(key, 172800); // 48小时过期
    }finally{
        if(jedis!=null){
            jedis.close();
        }
    }
    DecimalFormat df = new DecimalFormat("#0.00");
    System.out.println("Initialize"+$userId+$date+df.format(total));
end

rule "Check Daily Limit"
salience 10
no-loop true // 新增此行，防止重复执行
when
    $t: Transaction($userId: userId, $date: timestamp.toLocalDate(), $amount: amount, rejected == false)
    $ds: DailySummary(userId == $userId, date == $date, $total: totalAmount)
    eval($total + $amount > 10000.00)
then
    modify($t) { setRejected(true) };
    System.out.println("[风控触发] 用户 " + $userId + " 单日限额已达:" + ($total + $amount));
end

rule "Update Summary"
when
    $t: Transaction($userId: userId, $date: timestamp.toLocalDate(), $amount: amount, rejected == false)
    $ds: DailySummary(userId == $userId, date == $date)
then
    String key = "daily:" + $userId + ":" + $date;
    Jedis jedis = null;
    try{
        jedis = myRedisPool.getResource();
        jedis.incrByFloat(key, $amount);
        modify($ds) { setTotalAmount($ds.getTotalAmount() + $amount) };
    }finally{
        if(jedis!=null){
            jedis.close();
        }
    }
    retract($t);
    DecimalFormat df = new DecimalFormat("#0.00");
    System.out.println("[余额修改] 用户 " + $userId + " 单日累加至:"+df.format($ds.getTotalAmount()));
end